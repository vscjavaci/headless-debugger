apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'

sourceSets {
     main.java.srcDirs = ['../eclipse.jdt.ls/org.eclipse.jdt.ls.debug/src', 'headless-src/main/java']
     test.java.srcDirs = ['headless-src/test/java']
}

project.ext.checkstyleVersion = '8.1'
project.ext.sevntuChecksVersion = '1.24.2'

checkstyle {
    configFile file("../eclipse.jdt.ls/google_checks.xml")
    ignoreFailures = true
    checkstyleMain {
        source = sourceSets.main.allSource
    }

    checkstyleTest {
        enabled  = false
    }
}

repositories {
    maven { url "http://sevntu-checkstyle.github.com/sevntu.checkstyle/maven2"    }
    maven { url "http://mvnrepository.com/artifact" }
    maven {	url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    mavenCentral()
}

dependencies {
    compile group: 'com.github.javaparser', name: 'javaparser-core', version: '3.2.11'
    compile files("${System.getProperty('java.home')}/../lib/tools.jar")
    compile files("../eclipse.jdt.ls/org.eclipse.jdt.ls.core/target/org.eclipse.jdt.ls.core-0.2.0-SNAPSHOT.jar")
    compile files("lib/jdimodel.jar")
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.2'
    compile group: 'org.reactivestreams', name: 'reactive-streams', version: '1.0.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.core', version: '3.12.3'
    compile group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.launching', version: '3.8.101'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.4'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
    checkstyle "com.github.sevntu-checkstyle:sevntu-checks:${sevntuChecksVersion}"
}

eclipse.classpath.file {
  withXml{xml ->
    def node = xml.asNode()

    node.classpathentry.find{
      it.@kind == 'lib' && it.@path.contains('/tools.jar')
    }.@sourcepath = 'lib/jdk8u-jdi.zip'

    node.classpathentry.find{
      it.@kind == 'lib' && it.@path.contains('/org.eclipse.jdt.ls.core-0.2.0-SNAPSHOT.jar')
    }.@sourcepath = '../eclipse.jdt.ls/org.eclipse.jdt.ls.core/target/org.eclipse.jdt.ls.core-0.2.0-SNAPSHOT-sources.jar'
  }
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        showStandardStreams true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task ls(type: Exec) {
    commandLine 'cmd', '/c', 'build-languangeserver.bat'
}

